plugins {
    id "com.github.spotbugs" version "2.0.0" apply false
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'pmd'
    apply plugin: 'checkstyle'

    sourceCompatibility = 1.8
    repositories {
        jcenter()
    }
    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
        implementation 'edu.princeton.cs:algs4:1.0.4'
    }

    test {
        useJUnitPlatform()
    }

    spotbugs {
        excludeFilter new File("${rootDir}/config/algs4-spotbugs-filter.xml")
    }
    pmd {
        toolVersion "6.3.0"
        ruleSetFiles = files("${rootDir}/config/pmd.xml")
    }
    checkstyle {
        toolVersion "8.25"
    }

    ext.zipLocation = "$buildDir/${project.name}.zip"
    ext.suppliedProjectFolder = "$buildDir/suppliedProject"

    task downloadZipFile(type: Download) {
        url = "https://coursera.cs.princeton.edu/algs4/assignments/${project.name}/${project.name}.zip"
        dest = zipLocation
    }

    task unpack(type: Copy) {
        dependsOn downloadZipFile
        from zipTree(zipLocation)
        exclude ".idea/"
        into suppliedProjectFolder
    }

    task zipSolution(type: Zip) {
        archiveFileName = "solution.zip"
        destinationDirectory = file("$buildDir/")
        from "$projectDir/src/main/java"
    }

}
