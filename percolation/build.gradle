
test {
    useJUnitPlatform()
}

def zipLocation = "${buildDir}/percolation.zip"
def suppliedProjectFolder = "$buildDir/suppliedProject"

sourceSets {
    supplied {
        java {
            srcDir suppliedProjectFolder
        }
    }
}
dependencies {
    suppliedImplementation project(":percolation")
    suppliedImplementation 'edu.princeton.cs:algs4:1.0.4'
}


task downloadZipFile(type: Download) {
    url = 'https://coursera.cs.princeton.edu/algs4/assignments/percolation/percolation.zip'
    dest = zipLocation
}

task unpack(type: Copy) {
    dependsOn downloadZipFile
    from zipTree(zipLocation)
    exclude ".idea/"
    into suppliedProjectFolder
}
task runInteractivePercolationVisualizer(type: JavaExec) {
    dependsOn unpack
    classpath = sourceSets.supplied.runtimeClasspath
    main = 'InteractivePercolationVisualizer'
}

task runPercolationVisualizer(type: JavaExec) {
    dependsOn unpack
    classpath = sourceSets.supplied.runtimeClasspath
    main = 'PercolationVisualizer'
}

task zipSolution(type: Zip) {
    archiveFileName = "solution.zip"
    destinationDirectory = file("$buildDir/")
    from "$projectDir/src/main/java"
}