
test {
    useJUnitPlatform()
}

def zipLocation = "${buildDir}/percolation.zip"
def suppliedCodeFolder = "${buildDir}/suppliedCode"
sourceSets {
    supplied {
        java {
            srcDir suppliedCodeFolder
        }
    }
}
dependencies {
    suppliedImplementation project(":percolation")
    suppliedImplementation 'edu.princeton.cs:algs4:1.0.4'
}

import Download
import ExtractFilesFromZip

task('downloadZipFile', type: Download) {
    url = 'https://coursera.cs.princeton.edu/algs4/assignments/percolation/percolation.zip'
    dest = zipLocation
}

task('extractSourceFiles', type: ExtractFilesFromZip) {
    dependsOn downloadZipFile
    zipArchive = zipLocation
    dest = suppliedCodeFolder
    filenames = ["PercolationVisualizer.java", "InteractivePercolationVisualizer.java"]
}

task('runInteractivePercolationVisualizer', type: JavaExec) {
    dependsOn extractSourceFiles
    classpath = sourceSets.supplied.runtimeClasspath
    main = 'InteractivePercolationVisualizer'
}

task('runPercolationVisualizer', type: JavaExec) {
    dependsOn extractSourceFiles
    classpath = sourceSets.supplied.runtimeClasspath
    main = 'PercolationVisualizer'
}
