
// Build configuration for building and running the supplied java code,
// PercolationVisualizer.java and InteractivePercolationVisualizer.java
sourceSets {
    supplied {
        java {
            srcDir suppliedProjectFolder
        }
    }
}
dependencies {
    suppliedImplementation project(":percolation")
    suppliedImplementation 'edu.princeton.cs:algs4:1.0.4'
}
compileSuppliedJava {
    dependsOn unpack
}
task runInteractivePercolationVisualizer(type: JavaExec) {
    dependsOn compileSuppliedJava
    classpath = sourceSets.supplied.runtimeClasspath
    main = 'InteractivePercolationVisualizer'
}
task runPercolationVisualizer(type: JavaExec) {
    dependsOn compileSuppliedJava
    classpath = sourceSets.supplied.runtimeClasspath
    main = 'PercolationVisualizer'
}


// Add supplied test files to a resource folder for some tests.
ext.suppliedInputFiles = "$buildDir/inputfiles"
sourceSets {
    test {
        resources {
            srcDir suppliedInputFiles
        }
    }
}
task copyPercolatingTestFiles(type: Copy) {
    dependsOn unpack
    from suppliedProjectFolder
    into "$suppliedInputFiles/percolates"
    include "*.txt"
    exclude "*-no.txt", 'greeting57.txt', "heart25.txt"
}
task copyNotPercolatingTestFiles(type: Copy) {
    dependsOn unpack
    from suppliedProjectFolder
    into "$suppliedInputFiles/nopercolates"
    include "*-no.txt", 'greeting57.txt', 'heart25.txt'
}
task copyTestFiles() {
    dependsOn copyPercolatingTestFiles, copyNotPercolatingTestFiles
}
